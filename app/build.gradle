apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
    groupId = "com.plusauth.android"
    versionId = "1.0.0"
    artifactId = "oidc"
}
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName versionId

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    // okhttp version range must be in 3.12.x since that is the last version that supports
    // api level 16
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.12'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'


    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError = false
    title = null
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintrayUpload.dependsOn build

afterEvaluate {
    // Add dependency paths for javadoc
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompileProvider.get().classpath.files
    })
    publishing {
        publications {
            Release(MavenPublication) {
                artifact("$buildDir/outputs/aar/app-release.aar")

                groupId = groupId
                artifactId = artifactId
                version = versionId

                artifact sourcesJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included in the pom.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

bintray {
    user = properties.getProperty('BINTRAY_USER') ?:  System.getenv("BINTRAY_USER")
    key = properties.getProperty('BINTRAY_API_KEY') ?: System.getenv("BINTRAY_API_KEY")
    publications = ['Release']
    override = false
    pkg {
        repo = "maven"
        name = "plusauth-oidc-android"
        description = "PlusAuth OpenId Connect Library for Android"
        licenses = ['MIT']
        websiteUrl = "https://github.com/PlusAuth/plusauth-oidc-android"
        vcsUrl = "https://github.com/PlusAuth/plusauth-oidc-android.git"
        issueTrackerUrl = "https://github.com/PlusAuth/plusauth-oidc-android/issues"
        dryRun = false
        publicDownloadNumbers = true
        version {
            name = versionId
            desc = "PlusAuth OpenId Connect Library for Android"
            vcsTag = versionId
            released = new Date()
        }
    }
}